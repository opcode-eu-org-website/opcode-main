% Copyright (c) 2017-2020 Matematyka dla Ciekawych Świata (http://ciekawi.icm.edu.pl/)
% Copyright (c) 2017-2020 Robert Ryszard Paciorek <rrp@opcode.eu.org>
% 
% MIT License
% 
% Permission is hereby granted, free of charge, to any person obtaining a copy
% of this software and associated documentation files (the "Software"), to deal
% in the Software without restriction, including without limitation the rights
% to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
% copies of the Software, and to permit persons to whom the Software is
% furnished to do so, subject to the following conditions:
% 
% The above copyright notice and this permission notice shall be included in all
% copies or substantial portions of the Software.
% 
% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
% IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
% FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
% AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
% LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
% OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
% SOFTWARE.

% BEGIN: AWK
\subsection{awk}

Awk jest interpreterem prostego skryptowego języka umożliwiający przetwarzanie tekstowych baz danych postaci \texttt{linia=rekord}, gdzie pola oddzielane ustalonym separatorem (można powiedzieć że łączy funkcjonalność komend takich jak grep, cut, sed z prostym językiem programowania).

\teacher{Około 30 minutowe wprowadzenie z pisaniem kodu na żywo}

Wyżej zaprezentowane wypisanie 5 pola (rozdzielanego :) z pliku \Verb@/etc/passwd@  z eliminacją pustych linii oraz
linii złożonych tylko ze spacji i przecinków, realizowane przy użyciu poleceń \Verb@cut@ i \Verb@grep@
może być zrealizowane za pomocą samego awk:

\begin{CodeFrame*}[bash]{}
awk -F: '$5 !~ "^[ ,]*$" {print $5}' /etc/passwd
\end{CodeFrame*}

Awk daje duże możliwości przy przetwarzaniu tego typu tekstowych baz danych -- możemy np.
wypisywać wypisywać pierwsze pole w oparciu o warunki nałożone na inne:

\begin{CodeFrame*}[bash]{}
awk -F: '$5 !~ "^[ ,]*$" && $3 >= 1000 {print $1}' /etc/passwd
\end{CodeFrame*}

Jak widać w powyższych przykładach do poszczególnych pól odwołujemy się poprzez \Verb@$n@,
gdzie \Verb@n@ jest numerem pola, \Verb@$0@ oznacza cały rekord

Program dla każdego rekordu przetwarza kolejne instrukcje postaci \Verb@warunek { komendy }@,
instrukcji takich może być wiele w programie (przetwarzane są kolejno),
komenda \Verb@next@ kończy przetwarzanie danego rekordu.

Separator pola ustawiamy opcją \Verb@-F@ (lub zmienną \Verb@FS@),
domyślnym separatorem pola jest dowolny ciąg spacji i tabulatorów
(w odróżnieniu od cut separator może być wieloznakowym napisem lub wyrażeniem regularnym).
Domyślnym separatorem rekordu jest znak nowej linii (można go zmienić zmienną RS).

Awk jest prostym językiem programowania obsługującym podstawowe pętle i instrukcje warunkowe
oraz funkcje wyszukujące i modyfikujące napisy:

\begin{oframed}\noindent\shell{echo "aba aab bab baa bba bba" | awk}\Verb@ '{@\vspace{-0.95em}
\begin{minted}{awk}
	# dla każdego pola w rekordzie
	for (i=1; i<=NF; ++i) {
		# jeżeli jego numer jest parzysty
		# to zastąp wszystkie ciągi b pojedynczym B
		if (i%2==0)
			gsub("b+", "B", $i);
		
		# wyszukaj pozycję pod-napisu B
		ii = index($i, "B")
		# jeżeli znalazł
		# to wypisz pozycję i pod-napis od tej pozycji do końca
		if (ii)
			printf("# %d %s\n", ii, substr($i, ii))
		# zwróć uwagę że w AWK liczy elementy napisy od 1 a nie od 0
	}
	print $0
\end{minted}
\vspace{-0.95em}\Verb@}'@\end{oframed}

\noindent
AWK obsługuje także tablice asocjacyjne pozwala to np. policzyć powtórzenia słów:

\begin{oframed}\noindent\shell{echo "aa bb aa ee dd aa dd" | awk }\Verb@ '@\vspace{-0.95em}
\begin{minted}{awk}
	BEGIN {RS="[ \t\n]+"; FS=""}
	{slowa[$0]++}
	# może być kilka bloków {} pasujących do rekordu
	# jeżeli nie użyjemy next przetworzone zostaną wszystkie
	# {printf("rekord: %d done\n", NR)}
	END {for (s in slowa) printf("%s: %s\n", s, slowa[s])}
\end{minted}
\vspace{-0.95em}\Verb@'@\end{oframed}

Podobny efekt możemy uzyskać stosując "uniq -c" (który wypisuje unikalne wiersze wraz z ich ilością)
na odpowiednio przygotowanym napisie (spacje zastąpione nową linią, a linie posortowane):

\begin{CodeFrame*}[bash]{}
echo "aa bb aa ee dd aa dd" | tr ' ' '\n' | sort | uniq -c
\end{CodeFrame*}
Jednak rozwiązanie awk można łatwo zmodyfikować aby wypisywało pierwsze wystąpienie linii bez sortowania pliku.

Innym użytecznym zastosowaniem AWK może być wypisanie pliku bez linii pasujących do wzorca oraz linii poprzednich:

\begin{oframed}\noindent\shell{echo -e "aa\nbb\nWZORZEC\ncc" | awk}\Verb@ '{@\vspace{-0.95em}
\begin{minted}{awk}
	# dla linii pasującej do wzorca ustwaiamy flagę print_last na zero i przechodzimy do następnej linii
	/WZORZEC/ {print_last=0; next}
	# jeżeli flaga print_last jest nie zero wypisujemy zapamiętaną poprzednią liniię
	print_last == 1 {print last}
	# zapamiętujemy bierzacą linię do wypisania przy przetwarzaniu kolejnej (jeżeli nie bedzie pasować do wzorca)
	{last=$0; print_last=1}
	# jeżeli osiągneliśmy koniec pliku i mamy linię do wypisania to ją wypisujemy
	END {if (print_last == 1) print last}
\end{minted}
\vspace{-0.95em}\Verb@}'@\end{oframed}

AWK pozwala także na definiowanie funkcji:
\begin{CodeFrame*}[bash]{}
awk 'function f(x) {return 2*x} { print f($1+$2) }'
\end{CodeFrame*}
% END: AWK
